ACTOR FriendlyNPC
{
	//$Category "ClustBlooM Friendly Chars"
	//$Color 1
	Radius 24
	Height 48
	Health 50
	PainChance 255
	RenderStyle add
	Alpha 0.5
	+DONTTHRUST
	+FRIENDLY
	+SOLID
}

ACTOR FriendlyActor
{
	//$Category "ClustBlooM Friendly Chars"
	//$Color 1
	Health 1000
	Radius 20
	Height 56
	Speed 16
	PainChance 170
	Monster
	Species "Player"
	+DONTHARMSPECIES
	+FLOORCLIP
	+FRIENDLY
	+NOINFIGHTING
	+THRUSPECIES
}

ACTOR HoloHitler : FriendlyNPC 
{
	-SOLID
	States
	{
	Spawn:
		HITL AAAAABBBBBCCCCCDDDDDEEEEEFFFFFGGGGGHHHHHIIIII 1
		HITM AAAAABBBBBCCCCCDDDDDEEEEEFFFFFGGGGGHHHHHIIIII 1
		HITM IIIIIHHHHHGGGGGFFFFFEEEEEDDDDDCCCCCBBBBBAAAAA 1
		Loop
	Death:
		HITL AAAAABBBBBCCCCCDDDDDEEEEEFFFFFGGGGGHHHHHIIIII 1
		HITM AAAAABBBBBCCCCCDDDDDEEEEEFFFFFGGGGGHHHHHIIIII 1
		HITM IIIIIHHHHHGGGGGFFFFFEEEEEDDDDDCCCCCBBBBBAAAAA 1 A_FadeOut(0.01)
		Stop
	}
}

ACTOR NaziCaptured : FriendlyNPC
{
	Speed 0
	Health 300
	+FLOORCLIP
	States
	{
	Spawn:
		CAPT A 1
		CAPT ABC random(15,45) A_Stop
		Loop
	Yes:
		CAPT D 1 A_UnsetSolid
		TNT1 A 0 A_SpawnItemEx("FriendlyNazi",0,0,0,0)
		CAPT D -1
		Stop
	}
}

ACTOR FriendlyNazi : FriendlyActor
{
	SeeSound "wolfss/sight"
	PainSound "wolfss/pain"
	DeathSound "wolfss/death"
	ActiveSound "wolfss/active"
	AttackSound "wolfss/attack"
	Obituary "$OB_WOLFSS"
	Dropitem "Clip"
	States
	{
	Spawn:
		SSWV AB 10 A_Look
		Loop
	See:
		SSWV AABBCCDD 3 A_Chase
		Loop
	Missile:
		SSWV F 10 A_FaceTarget
		SSWV GF 4 Bright A_CPosAttack
		SSWV G 1 A_CPosRefire
		Goto Missile+1
	Pain:
		SSWV H 3
		SSWV H 3 A_Pain
		Goto See
	Death:
		SSWV I 5
		SSWV J 5 A_Scream
		SSWV K 5 A_NoBlocking
		SSWV L 5
		SSWV M -1
		Stop
	XDeath:
		SSWV N 5
		SSWV O 5 A_XScream
		SSWV P 5 A_NoBlocking
		SSWV QRSTU 5
		SSWV V -1
		Stop
	Raise:
		SSWV M 5
		SSWV LKJI 5
		Goto See
	}
}

ACTOR WhiteCells : FriendlyActor
{
	Health 400
	Speed 32
	PainChance 24
	SeeSound "wolfss/sight"
	PainSound "wolfss/pain"
	DeathSound "wolfss/death"
	ActiveSound "wolfss/active"
	AttackSound "wolfss/attack"
	Translation "112:127=80:95"
	DropItem "Medikit"
	+DROPOFF
	+LOOKALLAROUND
	+MISSILEEVENMORE
	+MISSILEMORE
	DamageFactor "Plasma",0.0
	BloodColor "Yellow"
	States
	{
	Spawn:
		PLAY A 1 A_Look
	Loop
	See:
		PLAY ABCD 4 A_Chase
		TNT1 A 0 A_CheckBlock("Jump")
		TNT1 A 0 A_JumpIfTargetInLOS("Missile")
		TNT1 A 0 A_CheckRange(3000.0,"Teleporter",true)
	Loop
	Missile:
		PLAY E 10 A_FaceTarget
		PLAY FE 4 Bright A_CustomMissile("PlasmaBallX")
		PLAY F 1 A_MonsterRefire(130, "See")
    Goto Missile+1
	Jump:
		PLAY A 1 A_Chase
		PLAY AABB 2 ThrustThingz(0,30,0,0)
		PLAY BBCC 2 A_Recoil(-6)
		PLAY DD 1
		Goto See
	Teleporter:
		PLAY A 1 A_CheckBlock("See",CBF_NOLINES)
		TNT1 A 0 A_SpawnItemEx("TeleportFog",0,0,0,0,0,0,0)
		PLAY B 1 A_Warp(AAPTR_PLAYER1,Random(40,60),Random(40,60),0,0,0,"Destination")
		Goto See
	Destination:
		TNT1 A 0 A_SpawnItemEx("TeleportFog",0,0,0,0,0,0,0)
		PLAY CD 2 A_Chase
		Goto See
	Pain:
		PLAY G 4
		PLAY G 4 A_Pain
	Goto See
	Death:
		PLAY H 10
		PLAY I 10 A_PlayerScream
		PLAY J 10 A_NoBlocking
		PLAY KLM 10
	DeathWait:
		PLAY N 1 A_CheckSight("DeathFade")
	Loop
	DeathFade:
		PLAY N 1 A_FadeOut(0.02)
	Loop
	XDeath:
		PLAY O 5
		PLAY P 5 A_XScream
		PLAY Q 5 A_NoBlocking
		PLAY RSTUV 5
		PLAY W -1
	XDeathWait:
		PLAY W 1 A_CheckSight("XDeathFade")
	Loop
	XDeathFade:
		PLAY W 1 A_FadeOut(0.02)
	Loop
  }
}

ACTOR PlasmaBallX: PlasmaBall
{
	Damagetype Plasma
	Species "Player"
	+DONTHARMSPECIES
	+THRUSPECIES
}