ACTOR Bonsai : BigTree
{
	//$Category "ClustBlooM Stuff"
	//$Color 3
	Scale 0.25
	Radius 8
	Height 27
}

ACTOR Ask
{
	//$Category "ClustBlooM Stuff"
	//$Color 3
	-SOLID
	+BRIGHT
	+FLOAT
	+NOGRAVITY
	Scale 0.1
	Height 8
	Radius 8
	States
	{
	Spawn:
		ASKX A 1
		Loop
	}
}

ACTOR Debris_Base //From Blade of Agony, Courtesy by Ozymandias81
{
	Radius 1
	Height 1
	Mass 5
	Projectile
	-ACTIVATEIMPACT
	-ACTIVATEPCROSS
	-NOGRAVITY
	+FLATSPRITE
	+RANDOMIZE
	+ROLLCENTER
	+ROLLSPRITE
	+THRUSPECIES
	Species "Player"
	BounceType "Doom"
	BounceFactor 0.3
	WallBounceFactor 0.3
	BounceCount 3
	Gravity 0.3
}

ACTOR Debris_GlassShard_Small : Debris_Base
{
	RenderStyle "Translucent"
	Alpha 0.4
	Scale 0.25
	-FLATSPRITE
	-ROLLCENTER
	-ROLLSPRITE
	States
	{
	Spawn:
		GLAS ABCD 4 NODELAY A_JumpIf(waterlevel == 3, "AdjustMass")
		Loop
	AdjustMass:
		"####" "#" 0 A_SetMass(400)
		Goto Swim
	Swim: //mxd
		"####" A 2 A_ScaleVelocity(0.7)
		"####" B 2
		"####" C 2 A_ScaleVelocity(0.7)
		"####" D 2
		Loop
	Death:
		"####" A 0 A_Jump(256,1,2,3)
		"####" ABCD 0 A_Jump(256,"Death1")
		Stop
	Death1:
		"####" "#" 0 {bBounceOnActors = FALSE;}
		"####" "#" 1 A_SetTics(35*(random(3,5)))
	Death1Wait:
		"####" "#" 1 A_FadeOut(0.1)
		Wait
	}
}

ACTOR Debris_GlassShard_Medium : Debris_GlassShard_Small { Scale 0.5 }
ACTOR Debris_GlassShard_Large : Debris_GlassShard_Small { Scale 1.0 }

ACTOR CoronaBeer
{
	//$Category "ClustBlooM Stuff"
	//$Color 3
	Radius 4
	Height 8
	Health 10
	Scale 0.05
	RenderStyle "Translucent"
	Alpha 0.95
	+SOLID
	+SHOOTABLE
	+NOBLOOD
	+NOBLOODDECALS
	States
	{
	Spawn:
		BEER A -1
		Stop
	Death:
		TNT1 A 0 A_PlaySound("GLASS5")
		"####" AAAAAAAA 0 A_SpawnItemEx("Debris_GlassShard_Small", 0, 0, 0, random(1,3), random(1,3), random(1,3), random(0,360), SXF_CLIENTSIDE)
		"####" A 0 A_UnsetSolid
		Stop
	}
}

//Straight from wiki - DAMAGE_THING special: (https://zdoom.org/wiki/DamageThing)
//"If amount is 0, then the thing is guaranteed to be killed regardless of any invulnerability (including God and Buddha)"
//So no 200 but 0 as Arg0; These actors comes from Blade of Agony - Ozy81
ACTOR Electricity1 : SwitchableDecoration
{
	//$Category "ClustBlooM Stuff"
	//$Title Electricity (large)
	//$Color 3
	//$Sprite ELECA0
	//$Arg0 "Harmless"
	//$Arg0Tooltip "Values greater than 0 make this actor harmless"
	Radius 16
	Height 128
	+BUMPSPECIAL
	+NOGRAVITY
	+RANDOMIZE
	DamageType "IceWater"
	Obituary "$OBFIZZLE"
	Activation THINGSPEC_MonsterTrigger
	States
	{
	Spawn:
	Active:
		ELEC A 0 NODELAY A_JumpIf(args[0] > 0, 2)
		"####" A 0 Thing_SetSpecial(0,73,0,22,0)
		"####" A 0 A_SetSolid
		"####" A 0 A_Jump(256, "ActiveAnim") // Goto doesn't work for inheritance - Talon1024
	ActiveAnim:
		ELEC A 0 A_Jump(128, 2)
		"####" ABCD 3
		Loop
	Inactive:
		TNT1 A 0 Thing_SetSpecial(0,0,0,0,0)
		"####" A 0 A_UnSetSolid
		"####" A 0 A_Jump(256, "OffAnim")
	OffAnim:
		"####" A 2
		Loop
	}
}

ACTOR Electricity2 : Electricity1
{
	//$Title Electricity (small)
	//$Sprite ELECE0
	Height 64
	States
	{
	ActiveAnim:
		ELEC EFGH 3
		Loop
	}
}

ACTOR Electricity3 : Electricity1
{
	//$Title Electricity (large, horizontal)
	//$Sprite ELECI0
	Height 16
	Radius 64
	States
	{
	ActiveAnim:
		ELEC IJKL 3
		Loop
	}
}

ACTOR ElectricOrb1 : Electricity1
{
	//$Title Electric Orb (big)
	//$Sprite LABZA0
	Height 16
	+NOGRAVITY
	States
	{
	ActiveAnim:
		LABZ BCDE 3
		Loop
	}
}

ACTOR ElectricOrb2 : Electricity1
{
	//$Title Electric Orb (small)
	//$Sprite LABZP0
	Height 16
	+NOGRAVITY
	States
	{
	ActiveAnim:
		LABZ PQRST 3
		Loop
	}
}