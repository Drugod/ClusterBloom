ACTOR RainSpawner : SwitchableDecoration
{
	//$Category "BlooM Weather"
	//$Title "Rain Spawner"
	//$Color 13
	//$Sprite RNDRA0
	//$Arg0 "Radius"
	//$Arg0Tooltip "With 128 for example you get a 256x256 quare or circle (See fourth argument)"
	//$Arg1 "Frequency"
	//$Arg1Tooltip "The lower the number, the heavier the rainfall"
	//$Arg2 "Sound"
	//$Arg2Type 11
	//$Arg2Enum { 0 = "Yes"; 1 = "No"; }
	//$Arg3 "Area Type"
	//$Arg3Type 11
	//$Arg3Enum { 0 = "Square"; 1 = "Circle"; }
	//$Arg4 "Raindrop Type"
	//$Arg4Type 11
	//$Arg4Enum { 0 = "Normal"; 1 = "Blood"; 2 = "Acid"; }
	Radius 1
	Height 1
	+CLIENTSIDEONLY
	+NOCLIP
	+NOGRAVITY
	+SPAWNCEILING
	States
	{
	Spawn:
	Active:
		TNT1 A 0 A_JumpIf(Args[2] == 1, "NoSound")
		TNT1 A 0 A_JumpIf(Args[3] == 1, "Circle")
		TNT1 A 0 A_JumpIf(Args[4] == 1, "BloodRain")
		TNT1 A 0 A_JumpIf(Args[4] == 2, "AcidRain")
		TNT1 A 0 A_PlaySoundEx("Ambient/Rain", "SoundSlot7",1, 0)
		TNT1 A 1 A_SpawnItemEx("RainDrop", frandom(-Args[0], Args[0]), frandom(-Args[0], Args[0]), -2, 0, 0, 0, 0, 128, Args[1])
		Loop
	Circle:
		TNT1 A 0 A_JumpIf(Args[4] == 1, "BloodRainCircle")
		TNT1 A 0 A_JumpIf(Args[4] == 2, "AcidRainCircle")
		TNT1 A 2 A_SpawnItemEx("RainDrop", frandom(-Args[0], Args[0]), 0, -2, 0, 0, 0, frandom(0, 360), 128, Args[1])
		Loop
	NoSound:
		TNT1 A 0 A_Jumpif(Args[3] == 1, "NoSoundCircle")
		TNT1 A 0 A_JumpIf(Args[4] == 1, "NoSoundBloodRain")
		TNT1 A 0 A_JumpIf(Args[4] == 2, "NoSoundAcidRain")
		TNT1 A 2 A_SpawnItemEx("RainDrop", frandom(-Args[0], Args[0]), frandom(-Args[0], Args[0]), -2, 0, 0, 0, 0, 128, Args[1])
		Loop
	NoSoundCircle:
		TNT1 A 2 A_SpawnItemEx("RainDrop", frandom(-Args[0], Args[0]), 0, -2, 0, 0, 0, frandom(0, 360), 128, Args[1])
		Loop
	AcidRainCircle:
		TNT1 A 1 A_SpawnItemEx("AcidDrop", frandom(-Args[0], Args[0]), 0, -2, 0, 0, 0, frandom(0, 360), 128, Args[1])
		Loop
	BloodRainCircle:
		TNT1 A 1 A_SpawnItemEx("BloodDrop", frandom(-Args[0], Args[0]), 0, -2, 0, 0, 0, frandom(0, 360), 128, Args[1])
		Loop
	AcidRain:
		TNT1 A 0 A_PlaySoundEx("Ambient/Rain", "SoundSlot7",1, 0)
		TNT1 A 1 A_SpawnItemEx("AcidDrop", frandom(-Args[0], Args[0]), frandom(-Args[0], Args[0]), -2, 0, 0, 0, 0, 128, Args[1])
		Loop
	BloodRain:
		TNT1 A 0 A_PlaySoundEx("Ambient/Rain", "SoundSlot7",1, 0)
		TNT1 A 1 A_SpawnItemEx("BloodDrop", frandom(-Args[0], Args[0]), frandom(-Args[0], Args[0]), -2, 0, 0, 0, 0, 128, Args[1])
		Loop
	NoSoundBloodRain:
		TNT1 A 1 A_SpawnItemEx("BloodDrop", frandom(-Args[0], Args[0]), frandom(-Args[0], Args[0]), -2, 0, 0, 0, 0, 128, Args[1])
		Loop
	NoSoundAcidRain:
		TNT1 A 1 A_SpawnItemEx("AcidDrop", frandom(-Args[0], Args[0]), frandom(-Args[0], Args[0]), -2, 0, 0, 0, 0, 128, Args[1])
		Loop
	}
}

ACTOR RainDrop
{
	Height 2
	Radius 2
	+DONTSPLASH
	+FORCEXYBILLBOARD
	+MISSILE
	+NOBLOCKMAP
	-NOGRAVITY
	States
	{
	Spawn:
		RNDR A 1 A_JumpIf(WaterLevel > 0, "Death")
		Loop
	Death:
		RNDR BCDEFGH 3 A_FadeOut(10.0)
		Stop
	}
}

ACTOR AcidDrop : RainDrop
{
	Damage 1
	States
	{
	Spawn:
		ACDR A 1 A_JumpIf(WaterLevel > 0, "Death")
		Loop
	Death:
		ACDR BCDEFGH 3 A_FadeOut(10.0)
		Stop
	}
}

ACTOR BloodDrop : RainDrop
{
	States
	{
	Spawn:
		BNDR A 1 A_JumpIf(WaterLevel > 0, "Death")
		Loop
	Death:
		BNDR BCDEFGH 3
		Stop
	}
}

ACTOR GreenFloorfog
{
	//$Category "ClustBlooM Fx"
	//$Color 5
	+FLOORCLIP
	+NOGRAVITY
	Renderstyle Add
	Alpha 0.58
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(128,"Spawn2")
	Spawn1:
		CVF2 A -1
		Stop
	Spawn2:
		CVF2 B -1
		Stop
	}
}

ACTOR Bubble
{
	Radius 2
	Height 2
	Speed 1
	Scale 0.05
	Alpha 0.25
	RenderStyle "Add"
	Projectile
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	+NOBLOCKMAP
	+NOCLIP
	States
	{
	Spawn:
		SBUB A 1
	Rise:
		SBUB A 0 A_JumpIf(waterlevel == 0, "Death")
		SBUB A 1 A_Jump (32, "Jiggle")
		Loop
	Jiggle:
		SBUB A 0 A_JumpIf(waterlevel == 0, "Death")
		SBUB A 1 A_SetAngle (random (0, 360))
		SBUB A 0 A_Recoil (0.1)
		Goto Rise
	Death:
		TNT1 A 1
		Stop
	}
}

ACTOR PlayerBubble : Bubble
{
	States
	{
	Spawn:
		SBUB A 1 NODELAY A_ChangeVelocity(0,0,random(1,3),CVF_RELATIVE)
		Goto Rise
	}
}

ACTOR BubbleSpawner
{
	//$Category "ClustBlooM Fx"
	//$Title Underwater Bubble Spawner
	//$Color 5
	//$Sprite SBUBA0
	Radius 2
	Height 2
	+NOINTERACTION
	States
	{
	Spawn:
		TNT1 A 0 NODELAY
		TNT1 A 0 A_CheckSightOrRange(1280,"Unsighted") //needed for more tweaks
		TNT1 A 2 A_SpawnItemEx("Bubble", random (-16, 16), 0, 0, 0, 0, 2, random (0, 360), 0, 128)
		Loop
	Unsighted:
		TNT1 A 25
		Goto Spawn
	}
}